*******************************************************
* Auto4n6: v0.03                                      *
*  Triage Collection Processor                        *
* Uses:                                               *
*  AChoirX, TriageReport                              *
*******************************************************
* Check Previous Run.  MUST BE BEFORE ANY PROCESSING  *
*******************************************************
CKY:&Acq
 SAY:[+] Processing Directory Already Exists: &Acq
 BYE:
END:
*******************************************************
*   Setup Syslog                                      *
*******************************************************
SET:CopyPath=Part
SET:SyslogS:127.0.0.1
SET:SyslogP=5514
SET:SyslogL=min
SAY:[+] Logging This Processing Run...
OPN:&Dir\Auto4n6.csv
OUT:"&Acn","&VR1","&Tim"
SAY:[+] Processing Triage Collection: &VR1 (Full Path: &VR0)
ACQ:\Triage
*******************************************************
* Log it to Prevent Running it again                  *
*******************************************************
OPN:ColDone.dat
OUT:&VR0
*******************************************************
* If the Move Didnt work, its probably still writing  *
* - Try 5 Times, Then Exit out                        *
*******************************************************
CN0:0
LBL:CopyCol
SAY:[+] Copying Collection: &VR1
SET:CopyPath=None
CPY:"&VR0" "&Acq"
SET:CopyPath=Part
CKN:&Acq\&VR1
 CN0:++
 N>>:&CN0 10
  SAY:[!] Max Retries (&CN0) Error Copying Collection File - Exiting...
  Bye:
 END:
SAY:[+] Retrying Copy (&CN0)...
SLP:180
JMP:CopyCol
END:
*******************************************************
* Fell Through - Process the Triage Collection        *
*******************************************************
SAY:[+] Decompressing Triage Collection: &VR1
*EXE:\Utils\7z\7za.exe x "&ACQ\&VR1" "-o&Acq" -r -y -aou
UNZ:&ACQ\&VR1 &Acq
SAY:[+] Triage Decompress Complete: &VR1
ACQ:\
SAY:[+] Running Timesketch Converter: &VR1
SYS:py &Dir\TriageReport\ts_Transform.py -d &Acq -c &Dir\TriageReport\ts_Transform.cfg
SAY:[+] Running Triage Report: &VR1
SYS:py &Dir\TriageReport\TriageReport.py -d &Acq -c &Dir\TriageReport\AChReport.cfg
SAY:[+] Copying Sort Javascript Library
CPY:"&Dir\TriageReport\sortable-Ach.js" "&Acq"
SAY:[+] Triage Report Completed: &VR1
SAY:{+} Running Plaso Timeliner: &VR1
INI:PlasoX.ACQ
